{-# LANGUAGE ScopedTypeVariables #-}

module Reactive.Plugins.Core.Action.MultiSelection where

import qualified Data.Set                          as Set
import           JS.MultiSelection                 (displaySelectionBox, hideSelectionBox)
import qualified JS.NodeGraph                      as UI
import           Utils.PreludePlus
import           Utils.Vector                      (Vector2 (..), x, y)

import           Object.Node
import           Object.Widget                     (DisplayObject, UIHandlers, WidgetFile, WidgetId, objectId)
import qualified Object.Widget.Node                as NodeModel

import           Event.Event                       (Event (Mouse, Keyboard), JSState)
import           Event.Keyboard                    (KeyMods (..))
import qualified Event.Keyboard                    as Keyboard
import qualified Event.Mouse                       as Mouse

import qualified Reactive.State.Camera             as Camera
import           Reactive.State.Global             (State)
import qualified Reactive.State.Global             as Global
import qualified Reactive.State.Graph              as Graph
import           Reactive.State.MultiSelection     (DragHistory (..))
import qualified Reactive.State.MultiSelection     as MultiSelection
import qualified Reactive.State.UIRegistry         as UIRegistry

import           Reactive.Commands.Command         (Command, performIO)
import           Reactive.Commands.Graph.Selection (focusSelectedNode, selectAll, selectedNodes, unselectAll)
import qualified Reactive.Commands.UIRegistry      as UICmd

import           Control.Monad.State               hiding (State)

import           UI.Raycaster                      (getObjectsInRect)

toAction :: Event -> Maybe (Command State ())
toAction (Mouse _ event@(Mouse.Event Mouse.Pressed  pos Mouse.LeftButton (KeyMods False False False False) Nothing)) = Just $ startDrag pos
toAction (Mouse jsstate event@(Mouse.Event Mouse.Moved    pos Mouse.LeftButton _ _)) = Just $ handleMove jsstate pos
toAction (Mouse _ event@(Mouse.Event Mouse.Released _   Mouse.LeftButton _ _)) = Just stopDrag

toAction (Keyboard _ (Keyboard.Event Keyboard.Press 'A'   _)) = Just trySelectAll
toAction (Keyboard _ (Keyboard.Event Keyboard.Down  '\27' _)) = Just tryUnselectAll
toAction _ = Nothing

trySelectAll :: Command State ()
trySelectAll = zoom Global.uiRegistry $ do
    focusedWidget <- use UIRegistry.focusedWidget
    when (isNothing focusedWidget) selectAll

tryUnselectAll :: Command State ()
tryUnselectAll = zoom Global.uiRegistry $ do
    focusedWidget <- use UIRegistry.focusedWidget
    when (isNothing focusedWidget) unselectAll

startDrag :: Vector2 Int -> Command State ()
startDrag coord = do
    Global.multiSelection . MultiSelection.history ?= (DragHistory coord coord)
    zoom Global.uiRegistry $ unselectAll

handleMove :: JSState -> Vector2 Int -> Command State ()
handleMove jsstate coord = do
    dragHistory <- use $ Global.multiSelection . MultiSelection.history
    case dragHistory of
        Nothing                          -> return ()
        Just (DragHistory start current) -> do
            Global.multiSelection . MultiSelection.history ?= DragHistory start coord
            updateSelection jsstate start coord
            drawSelectionBox start coord


lookupNode :: WidgetId -> Command UIRegistry.State (Maybe (WidgetFile NodeModel.Node))
lookupNode = UIRegistry.lookupTypedM

updateSelection :: JSState -> Vector2 Int -> Vector2 Int -> Command State ()
updateSelection jsstate start end = zoom Global.uiRegistry $ do
    let leftTop     = Vector2 (min (start ^. x) (end ^. x)) (min (start ^. y) (end ^. y))
        rightBottom = Vector2 (max (start ^. x) (end ^. x)) (max (start ^. y) (end ^. y))
        ids         = getObjectsInRect jsstate leftTop (rightBottom - leftTop)
    oldSelected <- selectedNodes
    newSelectedFiles <-  mapM lookupNode ids
    let oldSet = Set.fromList $ (view objectId) <$> oldSelected
        newSet = Set.fromList $ (view objectId) <$> catMaybes newSelectedFiles
        toSelect = Set.difference newSet oldSet
        toUnselect = Set.difference oldSet newSet

    forM_ toSelect   $ flip UICmd.update_ $ NodeModel.isSelected .~ True
    forM_ toUnselect $ flip UICmd.update_ $ NodeModel.isSelected .~ False


drawSelectionBox :: Vector2 Int -> Vector2 Int -> Command State ()
drawSelectionBox start end = do
    camera <- use $ Global.camera . Camera.camera
    let startSelectionBox = Camera.screenToWorkspace camera start
        endSelectionBox   = Camera.screenToWorkspace camera end
    performIO $ displaySelectionBox startSelectionBox endSelectionBox

stopDrag :: Command State ()
stopDrag = do
    wasSelecting <- uses (Global.multiSelection . MultiSelection.history) isJust
    when wasSelecting $ do
        Global.multiSelection . MultiSelection.history .= Nothing
        performIO hideSelectionBox
        zoom Global.uiRegistry focusSelectedNode
